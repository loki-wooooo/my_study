buildscript {
    ext {
        queryDslVersion='5.0.0'
    }
}

plugins {
    id 'java-library'
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

bootJar.enabled = false

dependencies {
    api 'org.springframework.boot:spring-boot-starter-data-jpa'


    // QueryDSL
    implementation "com.querydsl:querydsl-jpa:${queryDslVersion}:jakarta"
    implementation "com.querydsl:querydsl-core:${queryDslVersion}"

    annotationProcessor(
            "com.querydsl:querydsl-apt:${queryDslVersion}:jakarta",
            'jakarta.annotation:jakarta.annotation-api',
            'jakarta.persistence:jakarta.persistence-api'
    )

    runtimeOnly 'com.h2database:h2'

    implementation(project(":module-common"))
}

def querydslDir = layout.buildDirectory.dir('generated/querydsl').get()

// querydsl QClass 파일 생성 위치를 지정
tasks.withType(JavaCompile) {
    options.getGeneratedSourceOutputDirectory().set(querydslDir)
}

// java source set 에 querydsl QClass 위치 추가
sourceSets {
    main.java.srcDirs += [querydslDir]
}

// gradle clean 시에 QClass 디렉토리 삭제
clean {
    delete file(querydslDir)
}